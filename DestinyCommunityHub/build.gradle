buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.2'

    }
}

apply plugin: 'android'
apply plugin: 'crashlytics'
apply plugin: 'android-apt'

repositories {
    maven { url 'http://download.crashlytics.com/maven' }

}

dependencies {
    compile 'com.squareup.dagger:dagger:1.2.+'
    compile 'com.squareup:otto:1.3.4'
    compile 'com.squareup.picasso:picasso:2.1.1'
    compile 'com.etsy.android.grid:library:1.0.4'
    compile 'com.squareup.okhttp:okhttp:1.3.0'
    compile 'com.squareup.retrofit:retrofit:1.3.0'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.jakewharton.timber:timber:2.1.0'
    compile 'com.github.chrisbanes.actionbarpulltorefresh:library:+'
    compile 'com.crashlytics.android:crashlytics:1.+'
    compile 'com.android.support:support-v4:+'
    compile 'fr.nicolaspomepuy:discreetapprate:1.0.+@aar'
    compile fileTree(dir: 'libs', include: '*.jar')

    apt 'com.squareup.dagger:dagger-compiler:1.2.+'

    compile project(':libraries:slidinguppanel_library')
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.2'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 10
        versionName '1.09'
        packageName "com.opencabinetlabs.destinycommunityhub"

    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }


    buildTypes {
        debug {
            packageNameSuffix '.debug'
            runProguard false
        }
        release {

            runProguard true
            proguardFile 'proguard-rules.txt'
            proguardFile getDefaultProguardFile('proguard-android.txt')
            zipAlign true // this is default for release

        }
    }

}

def getSourceSetName(variant) {
    return new File(variant.dirName).getName();
}

android.applicationVariants.all { variant ->
    def aptOutputDir = project.file("build/source/apt")
    def aptOutput = new File(aptOutputDir, variant.dirName)
    println "****************************"
    println "variant: ${variant.name}"
    println "manifest:  ${variant.processResources.manifestFile}"
    println "aptOutput:  ${aptOutput}"
    println "****************************"

    android.sourceSets[getSourceSetName(variant)].java.srcDirs += aptOutput.getPath()

    variant.javaCompile.options.compilerArgs += [
            '-processorpath', configurations.apt.getAsPath(),
            '-AndroidManifestFile=' + variant.processResources.manifestFile,
            '-s', aptOutput
    ]

    variant.javaCompile.source = variant.javaCompile.source.filter { p ->
        return !p.getPath().startsWith(aptOutputDir.getPath())
    }

    variant.javaCompile.doFirst {
        aptOutput.mkdirs()
    }
}


